apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// ATENCAO, NAO FUNCIONA, POR ISSO, JOGA EXCECAO:
// Class 'ActionGameToScore' is not abstract and does not implement abstract member public abstract val actionId: Int defined in androidx.navigation.NavDirections
//apply plugin: 'androidx.navigation.safeargs.kotlin' // Codelabs -> ViewModel and ViewModelFactory

// POR ISSO OPTEI POR ESSA SOLUCAO, ATE ELES ENTREGAREM UM FIX
apply plugin: 'androidx.navigation.safeargs' // Codelabs -> ViewModel and ViewModelFactory

// +-----------------------------------------------------------------+
// | Annotation processor for Room                                   |
// +-----------------------------------------------------------------+
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "br.com.programadordeelite.gdc"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // +-----------------------------------------------------------------+
        // | Workmanager                                                     |
        // +-----------------------------------------------------------------+
        renderscriptTargetApi 11
        renderscriptSupportModeEnabled true
        vectorDrawables {
            useSupportLibrary true
        }
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    signingConfigs {
        release {
            storeFile file("release-key.keystore")
            storePassword 'password'
            keyAlias 'alias'
            keyPassword 'journaldev'
        }
    }
    // +-----------------------------------------------------------------+
    // | Build types you'll encounter in every projects (Build Variants) |
    // +-----------------------------------------------------------------+
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        // debug don't need to be specified, only if you want to change something
        debug {
            pseudoLocalesEnabled true // para exibir idiomas fakes no seu dispositivo
            minifyEnabled false // para nao compactar o código durante desenvolvimento
            debuggable true // para poder usar break points e depurar valores
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        /**
         * The `initWith` property allows you to copy configurations from other build types,
         * then configure only the settings you want to change. This one copies the debug build
         * type, and then changes the manifest placeholder and application ID.
         */
        staging {
            initWith debug
            manifestPlaceholders = [hostName:"internal.softsuit.com"]
            applicationIdSuffix ".debugStaging"
        }
    }

    // +-----------------------------------------------------------------+
    // | Java Compile Options                                            |
    // +-----------------------------------------------------------------+
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    // +-----------------------------------------------------------------+
    // | exclude atomic functions module from package prevent warnings   |
    // +-----------------------------------------------------------------+
    packagingOptions {
        // A PARTIR DE GRADLE 7.0.2 PODE SER ASSIM TAMBEM
        // resources.excludes.add("META-INF/*")
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }

    // +-----------------------------------------------------------------+
    // | Enables view binding                                            |
    // +-----------------------------------------------------------------+
    viewBinding {
        enabled = true
    }

    useLibrary("android.test.runner")
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.31"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'

    // +-----------------------------------------------------------------+
    // | Layouting                                                       |
    // +-----------------------------------------------------------------+
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // +-----------------------------------------------------------------+
    // | Workmanager                                                     |
    // +-----------------------------------------------------------------+
    implementation "androidx.work:work-runtime-ktx:2.7.0"
    implementation "com.github.bumptech.glide:glide:4.10.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    // +-----------------------------------------------------------------+
    // | com.android... are old, using material instead                  |
    // +-----------------------------------------------------------------+
    // implementation "com.android.support:design:26.1.0"

    // +-----------------------------------------------------------------+
    // | TabExperience - UPDATE                                          |
    // +-----------------------------------------------------------------+
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // +-----------------------------------------------------------------+
    // | Material Components                                             |
    // +-----------------------------------------------------------------+
    implementation 'com.google.android.material:material:1.5.0-alpha04'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.31"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // +-----------------------------------------------------------------+
    // | startFragmentForResult (setFragmentResultListener)              |
    // +-----------------------------------------------------------------+
    implementation "androidx.fragment:fragment-ktx:1.3.6"

    // +-----------------------------------------------------------------+
    // | Codelab App Settings                                            |
    // +-----------------------------------------------------------------+
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // +-----------------------------------------------------------------+
    // | Room with view                                                  |
    // +-----------------------------------------------------------------+
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"

    // +-----------------------------------------------------------------+
    // | Coroutines                                                      |
    // +-----------------------------------------------------------------+
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0"

    implementation "androidx.room:room-runtime:2.3.0"
    implementation "androidx.room:room-ktx:2.3.0"
    kapt "org.xerial:sqlite-jdbc:3.34.0"
    kapt "androidx.room:room-compiler:2.3.0"
    implementation "androidx.activity:activity-ktx:1.3.1"

    // +-----------------------------------------------------------------+
    // | Paging 3 Library                                                |
    // +-----------------------------------------------------------------+
    implementation("androidx.paging:paging-runtime-ktx:3.0.1")
    // alternatively - without Android dependencies for tests
    //testImplementation("androidx.paging:paging-common-ktx:3.0.0")
    // optional - RxJava2 support
    //implementation("androidx.paging:paging-rxjava2-ktx:3.0.0")
    // optional - RxJava3 support
    //implementation("androidx.paging:paging-rxjava3:3.0.0")
    // optional - Guava ListenableFuture support
    //implementation("androidx.paging:paging-guava:3.0.0")

    // +-----------------------------------------------------------------+
    // | Retrofit - Paging 3 Library                                     |
    // +-----------------------------------------------------------------+
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:retrofit-mock:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"

    // +-----------------------------------------------------------------+
    // | Accessibility                                                   |
    // +-----------------------------------------------------------------+
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:3.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.4.0" // deprecated
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3"   // use this instead now

    // +-----------------------------------------------------------------+
    // | Navigation                                                      |
    // +-----------------------------------------------------------------+
    def nav_version = "2.3.5"
    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version" //Codelabs -> ViewModel and ViewModelFactory
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version" //Codelabs -> ViewModel and ViewModelFactory
    // Feature module Support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    // Jetpack Compose Integration
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha10"

    // +-----------------------------------------------------------------+
    // | Depuração deps                                                  |
    // +-----------------------------------------------------------------+
    implementation "com.jakewharton.timber:timber:4.7.1"

    // +-----------------------------------------------------------------+
    // | Testing deps                                                    |
    // +-----------------------------------------------------------------+
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.room:room-testing:2.3.0"

    // para testar viewModel e LiveData na pasta "test"
    testImplementation "androidx.test:core-ktx:1.4.1-alpha03"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.robolectric:robolectric:4.5.1"
    testImplementation "androidx.test.ext:junit:1.1.3"

    // para testar viewModel e LiveData na pasta "androidTest"
    androidTestImplementation "androidx.test:core-ktx:1.4.1-alpha03"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation 'androidx.test:runner:1.4.1-alpha03'
//    androidTestImplementation("org.robolectric:robolectric:4.5.1")
//    {
//        exclude group: 'org.apache.maven', module: 'maven-artifact'
//        exclude group: 'org.apache.maven', module: 'maven-artifact-manager'
//        exclude group: 'org.apache.maven', module: 'maven-model'
//        exclude group: 'org.apache.maven', module: 'maven-plugin-registry'
//        exclude group: 'org.apache.maven', module: 'maven-profile'
//        exclude group: 'org.apache.maven', module: 'maven-project'
//        exclude group: 'org.apache.maven', module: 'maven-settings'
//        exclude group: 'org.apache.maven', module: 'maven-error-diagnostics'
//        exclude group: 'org.apache.maven', module: 'maven-ant-tasks'
//        exclude group: "org.apache.maven.wagon"
//        exclude group: "org.codehaus.plexus"
//    }

    // +-----------------------------------------------------------------+
    // | Dependências para testes simulados com mockito                  |
    // +-----------------------------------------------------------------+
    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation "org.mockito:mockito-core:2.28.2"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:2.28.1"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.4.0"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1"


    // +-----------------------------------------------------------------+
    // | CUIDADO!!!! ESSA DEPENDENCIA AQUI FAZ COM QUE OS TESTE FALHEM   |
    // +-----------------------------------------------------------------+
    //   TESTE FICA RODANDO SEM NUNCA PARAR! NAO USAR!!!! >>> debugImplementation "androidx.fragment:fragment-testing:1.3.6"
    //   JA EXISTE UM TICKET OFICIAL ABERTO SEM RESOLUçÃO AINDA: Once https://issuetracker.google.com/128612536 is fixed this can be fixed.

    androidTestImplementation ("androidx.test.espresso:espresso-core:3.4.0", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
}